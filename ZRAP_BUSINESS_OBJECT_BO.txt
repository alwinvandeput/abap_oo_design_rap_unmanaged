CLASS zrap_business_object_bo DEFINITION
  PUBLIC
  ABSTRACT
  CREATE PUBLIC .

  PUBLIC SECTION.

    METHODS constructor.

    METHODS get_key_string
      ABSTRACT
      RETURNING VALUE(key) TYPE string.

  PROTECTED SECTION.

    TYPES t_persistence_state TYPE string.

    CONSTANTS:
      BEGIN OF persistence_states,
        new        TYPE t_persistence_state VALUE 'New',
        update     TYPE t_persistence_state VALUE 'Update',
        read       TYPE t_persistence_state VALUE 'Read',
        delete     TYPE t_persistence_state VALUE 'Delete',
        new_delete TYPE t_persistence_state VALUE 'New delete',
      END OF persistence_states.

    METHODS _rap_get_state
      RETURNING VALUE(state) TYPE t_persistence_state.

    METHODS _rap_set_read.

    METHODS _rap_set_update.

    METHODS _rap_set_delete.

  PRIVATE SECTION.

    DATA persistence_state TYPE t_persistence_state.

ENDCLASS.

CLASS zrap_business_object_bo IMPLEMENTATION.

  METHOD constructor.

    persistence_state = persistence_states-new.

  ENDMETHOD.

  METHOD _rap_get_state.

    state = persistence_state.

  ENDMETHOD.

  METHOD _rap_set_read.

    persistence_state = persistence_states-read.

  ENDMETHOD.

  METHOD _rap_set_update.

    CASE persistence_state.

      WHEN persistence_states-new OR
           persistence_states-update OR
           persistence_states-delete OR
           persistence_states-new_delete.

      WHEN persistence_states-read.
        persistence_state = persistence_states-update.

      WHEN OTHERS.

    ENDCASE.

  ENDMETHOD.

  METHOD _rap_set_delete.

    CASE persistence_state.

      WHEN persistence_states-new.
        persistence_state = persistence_states-new_delete.

      WHEN OTHERS.
        persistence_state = persistence_states-delete.

    ENDCASE.

  ENDMETHOD.

ENDCLASS.